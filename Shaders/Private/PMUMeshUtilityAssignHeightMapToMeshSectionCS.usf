////////////////////////////////////////////////////////////////////////////////
//
// MIT License
// 
// Copyright (c) 2018-2019 Nuraga Wiswakarma
// 
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
// 
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
// 
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.
//
////////////////////////////////////////////////////////////////////////////////
//

/*------------------------------------------------------------------------------
	Compile time parameters:
		THREAD_SIZE_X - The number of threads (x) to launch per workgroup
		THREAD_SIZE_Y - The number of threads (y) to launch per workgroup
------------------------------------------------------------------------------*/

#include "/Engine/Private/Common.ush"

RWBuffer<float> OutPositionData;
RWByteAddressBuffer OutTangentData;

Buffer<float4> ColorData;
Buffer<float2> UVData;
Buffer<float4> TangentData;

Texture2D HeightTexture;
SamplerState HeightTextureSampler;

uint _VertexCount;
float _HeightScale;

float2 _UVScale;
float _AlongTangents;

float _MaskByColor;
float _InverseMask;
float4 _ColorMask;

#ifndef PMU_MESH_UTILITY_ASSIGN_HEIGHT_MAP_USE_UV_BUFFER
#define PMU_MESH_UTILITY_ASSIGN_HEIGHT_MAP_USE_UV_BUFFER 0
#endif

#ifndef PMU_MESH_UTILITY_ASSIGN_HEIGHT_MAP_ASSIGN_TANGENTS
#define PMU_MESH_UTILITY_ASSIGN_HEIGHT_MAP_ASSIGN_TANGENTS 0
#endif

#define TAN_STRIDE 8

uint4 U32ToU8x4(uint v)
{
    uint4 unpacked;
    unpacked.x = ((v >>  0) & 0xFF);
    unpacked.y = ((v >>  8) & 0xFF);
    unpacked.z = ((v >> 16) & 0xFF);
    unpacked.w = ((v >> 24) & 0xFF);
    return unpacked;
}

int4 U32ToI8x4(uint v)
{
    int4 unpacked;
    unpacked.x = ((v >>  0) & 0xFF);
    unpacked.y = ((v >>  8) & 0xFF);
    unpacked.z = ((v >> 16) & 0xFF);
    unpacked.w = ((v >> 24) & 0xFF);
    return unpacked;
}

float4 U8x4ToSNF32x4(uint4 v)
{
    return (v/255.0f)*2.0f-1.0f;
}

float4 I8x4ToSNF32x4(int4 v)
{
    return v/127.0f;
}

uint PackNormalizedFloat4(float4 v)
{
	int4 n;
    n = v * 127.4999f;
    n = n & 0xFF;
	return n.x | (n.y << 8) | (n.z << 16) | (n.w << 24);
}

[numthreads(THREAD_SIZE_X,1,1)]
void AssignHeightMap_PositionOnlyCS(uint3 tid : SV_DispatchThreadID)
{
	const uint VertexId = tid.x;

    // Skip out of bounds threads
    if (VertexId >= _VertexCount)
	{
		return;
	}

    const uint PosAttrId = 3 * VertexId;

    uint ColorId = VertexId * (_MaskByColor>.5f);
    float ColorMask;
    ColorMask = dot(ColorData[ColorId], _ColorMask);
    ColorMask = (_InverseMask>.5f) ? (1.f-ColorMask) : ColorMask;

    float2 UV;

#if PMU_MESH_UTILITY_ASSIGN_HEIGHT_MAP_USE_UV_BUFFER
    UV = UVData[VertexId].xy * _UVScale;
#else
    UV.x = OutPositionData[PosAttrId  ];
    UV.y = OutPositionData[PosAttrId+1];
    UV *= _UVScale;
#endif

    float hv;
    hv = Texture2DSample(HeightTexture, HeightTextureSampler, UV).x;

#if PMU_MESH_UTILITY_ASSIGN_HEIGHT_MAP_ASSIGN_TANGENTS
    const uint TanAttrId = 2 * VertexId;
    const float2 uvo = _UVScale;

// 4 Samples
#if 1
    float hpx = Texture2DSample(HeightTexture, HeightTextureSampler, UV+float2(uvo.x, 0.f)).x;
    float hpy = Texture2DSample(HeightTexture, HeightTextureSampler, UV+float2(0.f, uvo.y)).x;
    float hnx = Texture2DSample(HeightTexture, HeightTextureSampler, UV-float2(uvo.x, 0.f)).x;
    float hny = Texture2DSample(HeightTexture, HeightTextureSampler, UV-float2(0.f, uvo.y)).x;

    float3 nx = float3(1,0, (hpx-hnx)*_HeightScale);
    float3 ny = float3(0,1, (hpy-hny)*_HeightScale);
    float3 normal = normalize(cross(nx, ny));
// 3 Samples
#else
    float hx = Texture2DSample(HeightTexture, HeightTextureSampler, UV+float2(uvo.x, 0.f)).x;
    float hy = Texture2DSample(HeightTexture, HeightTextureSampler, UV+float2(0.f, uvo.y)).x;

    float3 nx = float3(1,0, (hx-hv)*_HeightScale);
    float3 ny = float3(0,1, (hy-hv)*_HeightScale);
    float3 normal = normalize(cross(nx, ny));
#endif

    uint ut = PackNormalizedFloat4(float4(1,0,0,0));
    uint un = PackNormalizedFloat4(float4(normal,1.f));

    OutTangentData.Store2(VertexId*TAN_STRIDE, uint2(ut,un));
#endif

    uint TangentZId = 2 * VertexId * (_AlongTangents>.5f) + 1;
    float3 HeightDirection = TangentData[TangentZId].xyz;
    float3 HeightOffset = hv * _HeightScale * ColorMask * HeightDirection;

    OutPositionData[PosAttrId  ] += HeightOffset.x;
    OutPositionData[PosAttrId+1] += HeightOffset.y;
    OutPositionData[PosAttrId+2] += HeightOffset.z;
}
